
/**
 * @file Environment_Monitoring.ino
 * @author Baxter Yazbek (based on RAKWireless and others)
 * @brief This sketch demonstrate how to get environment data from BME680
 *    and send the data to thethingsnetwork
 * @version 0.1
 * @date 2020-27.12.2020
 * 

//---------------------------------------------------------
// Decoding in the TTN console:
//---------------------------------------------------------
/**
function Decoder(bytes, port) {
  // Decode an uplink message from a buffer
  // (array) of bytes to an object of fields.
  var decoded = {};

  // if (port === 1) decoded.led = bytes[0];function Decoder(bytes, port) {
  var TEMP    = (bytes[1] << 8)  + bytes[2];
  var HUM     = (bytes[3] << 8)  + bytes[4];  
  var PRESS   = (bytes[5] <<24) + (bytes[6] <<16) + (bytes[7] <<8)   + bytes[8];
  var GAS     = (bytes[9] <<24) + (bytes[10] <<16) + (bytes[11] <<8)   + bytes[12];
  
  return {
  Temperature:     TEMP,
  Humidity:        HUM,
  Pressure:        PRESS,
  GAS:             GAS,
  };
  
}
*/
//---------------------------------------------------------

#include <Arduino.h>
#include <LoRaWan-RAK4630.h> //http://librarymanager/ALL#SX126x-Arduino
#include <SPI.h>

#include <Wire.h>
#include "ClosedCube_BME680.h" //http://librarymanager/ALL#ClosedCube_BME680_Arduino

ClosedCube_BME680 bme680;

// RAK4630 supply two LED

//greenled
#ifndef LED_BUILTIN
#define LED_BUILTIN 35
#endif
//blueled
#ifndef LED_BUILTIN2
#define LED_BUILTIN2 36
#endif

//---------------------------------------------------------
// LoRaWan Variables 
//---------------------------------------------------------
bool doOTAA = true;
#define REGION_EU868
#define SCHED_MAX_EVENT_DATA_SIZE APP_TIMER_SCHED_EVENT_DATA_SIZE /**< Maximum size of scheduler events. */
#define SCHED_QUEUE_SIZE 60										  /**< Maximum number of events in the scheduler queue. */
#define LORAWAN_DATERATE DR_5									  /*LoRaMac datarates definition, from DR_0 to DR_5*/
#define LORAWAN_TX_POWER TX_POWER_10								  /*LoRaMac tx power definition, from TX_POWER_0 to TX_POWER_15*/
#define JOINREQ_NBTRIALS 3										  /**< Number of trials for the join request. */
#define LORAWAN_ADR LORAWAN_ADR_OFF   /*ADR on will cause downlink by default o adjust ADR*/
DeviceClass_t gCurrentClass = CLASS_A;							  /* class definition*/
lmh_confirm gCurrentConfirm = LMH_UNCONFIRMED_MSG;          /* confirm/unconfirm packet definition---changed from LMH_CONFIRMED_MSG */ 
uint8_t gAppPort = LORAWAN_APP_PORT;							  /* data port*/

/**@brief Structure containing LoRaWan parameters, needed for lmh_init()
 */
static lmh_param_t lora_param_init = {LORAWAN_ADR, LORAWAN_DATERATE, LORAWAN_PUBLIC_NETWORK, JOINREQ_NBTRIALS, LORAWAN_TX_POWER, LORAWAN_DUTYCYCLE_OFF};

// Foward declaration
static void lorawan_has_joined_handler(void);
static void lorawan_rx_handler(lmh_app_data_t *app_data);
static void lorawan_confirm_class_handler(DeviceClass_t Class);
static void send_lora_frame(void);

/**@brief Structure containing LoRaWan callback functions, needed for lmh_init()
*/
static lmh_callback_t lora_callbacks = {BoardGetBatteryLevel, BoardGetUniqueId, BoardGetRandomSeed,
										lorawan_rx_handler, lorawan_has_joined_handler, lorawan_confirm_class_handler};

//---------------------------------------------------------
// thethingsnetwork Variables / keys -> replace here
//---------------------------------------------------------
//OTAA keys all MSB ->> copy and paste MSB from TTN
uint8_t nodeDeviceEUI[8] = { 0x00, XX };
uint8_t nodeAppEUI[8] = { XX };
uint8_t nodeAppKey[16] = { XX };
#define LORAWAN_APP_DATA_BUFF_SIZE 64	  /**< buffer size of the data to be transmitted. */
#define LORAWAN_APP_INTERVAL 300000	  /**< Defines for user timer, the application data transmission interval. 20s, value in [ms]. */
//---------------------------------------------------------

static uint8_t m_lora_app_data_buffer[LORAWAN_APP_DATA_BUFF_SIZE];			  //< Lora user application data buffer.
static lmh_app_data_t m_lora_app_data = {m_lora_app_data_buffer, 0, 0, 0, 0}; //< Lora user application data structure.

TimerEvent_t appTimer;
static uint32_t timers_init(void);
static uint32_t count = 0;
static uint32_t count_fail = 0;

//#define my_DEBUG  //comment if on battery
//---------------------------------------------------------
// Debugging included 
//---------------------------------------------------------
#ifdef my_DEBUG
#define DEBUG_WRT(...) { Serial.write(__VA_ARGS__); }
#define DEBUG_PRT(...) { Serial.print(__VA_ARGS__); }
#define DEBUG_PLN(...) { Serial.println(__VA_ARGS__); }
#define DEBUG_PRF(...) { Serial.printf(__VA_ARGS__); }
#define DEBUG_BEGIN()  { Serial.begin(115200); }
#else
#define DEBUG_WRT(...) {}
#define DEBUG_PRT(...) {}
#define DEBUG_PLN(...) {}
#define DEBUG_PRF(...) {}
#define DEBUG_BEGIN() {}
#endif


void setup()
{
	pinMode(LED_BUILTIN, OUTPUT);
	digitalWrite(LED_BUILTIN, LOW);
  pinMode(LED_BUILTIN2, OUTPUT);
  digitalWrite(LED_BUILTIN2, LOW);

  
	// Initialize LoRa chip.
	lora_rak4630_init();

	// Initialize Serial for debug output
	DEBUG_BEGIN();

  DEBUG_PLN("=====================================");
  DEBUG_PLN("Welcome to RAK4630 LoRaWan!!!");
  DEBUG_PLN("Type: OTAA");
//---------------------------------------------------------
// LoRaWan region
//---------------------------------------------------------  
#if defined(REGION_AS923)
  DEBUG_PLN("Region: AS923");
#elif defined(REGION_AU915)
	DEBUG_PLN("Region: AU915");
#elif defined(REGION_CN470)
	DEBUG_PLN("Region: CN470");
#elif defined(REGION_CN779)
	DEBUG_PLN("Region: CN779");
#elif defined(REGION_EU433)
	DEBUG_PLN("Region: EU433");
#elif defined(REGION_IN865)
	DEBUG_PLN("Region: IN865");
#elif defined(REGION_EU868)
	DEBUG_PLN("Region: EU868");
#elif defined(REGION_KR920)
	DEBUG_PLN("Region: KR920");
#elif defined(REGION_US915)
	DEBUG_PLN("Region: US915");
#elif defined(REGION_US915_HYBRID)
	DEBUG_PLN("Region: US915_HYBRID");
#else
	DEBUG_PLN("Please define a region in the compiler options.");
#endif
	DEBUG_PLN("=====================================");
//  DEBUG_PLN("Battery:", BoardGetBatteryLevel);
	/* bme680 init */
  digitalWrite(LED_BUILTIN, HIGH);
	bme680_init();
  digitalWrite(LED_BUILTIN, LOW);

	//creat a user timer to send data to server period
	uint32_t err_code;

	err_code = timers_init();
	if (err_code != 0)
	{
		DEBUG_PRF("timers_init failed - %d\n", err_code);
	}

	// Setup the EUIs and Keys
	lmh_setDevEui(nodeDeviceEUI);
	lmh_setAppEui(nodeAppEUI);
	lmh_setAppKey(nodeAppKey);

	// Initialize LoRaWan
	err_code = lmh_init(&lora_callbacks, lora_param_init, doOTAA);
	if (err_code != 0)
	{
		DEBUG_PRF("lmh_init failed - %d\n", err_code);
	}

	// Start Join procedure

	lmh_join();
}

void loop()
{
	// Handle Radio events
	Radio.IrqProcess();
}

/**@brief LoRa function for handling HasJoined event.
 */
void lorawan_has_joined_handler(void)
{
	DEBUG_PLN("OTAA Mode, Network Joined!");
  delay(2000);

	lmh_error_status ret = lmh_class_request(gCurrentClass);
	if (ret == LMH_SUCCESS)
	{
  digitalWrite(LED_BUILTIN2, HIGH);
		delay(1000);
  digitalWrite(LED_BUILTIN2, LOW);
		TimerSetValue(&appTimer, LORAWAN_APP_INTERVAL);
		TimerStart(&appTimer);
	}
}

/**@brief Function for handling LoRaWan received data from Gateway
 *
 * @param[in] app_data  Pointer to rx data
 */
void lorawan_rx_handler(lmh_app_data_t *app_data)
{
	DEBUG_PRF("LoRa Packet received on port %d, size:%d, rssi:%d, snr:%d, data:%s\n",
				  app_data->port, app_data->buffsize, app_data->rssi, app_data->snr, app_data->buffer);
}

void lorawan_confirm_class_handler(DeviceClass_t Class)
{
	DEBUG_PRF("switch to class %c done\n", "ABC"[Class]);
	// Informs the server that switch has occurred ASAP
	m_lora_app_data.buffsize = 0;
	m_lora_app_data.port = gAppPort;
	lmh_send(&m_lora_app_data, gCurrentConfirm);
}

void send_lora_frame(void)
{
	if (lmh_join_status_get() != LMH_SET)
	{
		//Not joined, try again later
  DEBUG_PRT("join failed");
		return;
	}
  digitalWrite(LED_BUILTIN2, HIGH);
	bme680_get();
  digitalWrite(LED_BUILTIN2, LOW);
  
	lmh_error_status error = lmh_send(&m_lora_app_data, gCurrentConfirm);
	if (error == LMH_SUCCESS)
	{

		count++;
		DEBUG_PRF("lmh_send ok count %d\n", count);
  digitalWrite(LED_BUILTIN2, LOW);
	}
	else
	{
		count_fail++;
		DEBUG_PRF("lmh_send fail count %d\n", count_fail);
	}
}

/**@brief Function for handling user timerout event.
 */
void tx_lora_periodic_handler(void)
{
  digitalWrite(LED_BUILTIN2, HIGH);
	TimerSetValue(&appTimer, LORAWAN_APP_INTERVAL);
	TimerStart(&appTimer);
	DEBUG_PLN("Sending frame now...");
	send_lora_frame();
}

/**@brief Function for the Timer initialization.
 *
 * @details Initializes the timer module. This creates and starts application timers.
 */
uint32_t timers_init(void)
{
	TimerInit(&appTimer, tx_lora_periodic_handler);
	return 0;
}

void bme680_init()
{
	Wire.begin();
	bme680.init(0x76); // I2C address: 0x76 or 0x77
	bme680.reset();

	DEBUG_PRT("Chip ID=0x");
	DEBUG_PLN(bme680.getChipID(), HEX);

	// oversampling: humidity = x1, temperature = x2, pressure = x16
	bme680.setOversampling(BME680_OVERSAMPLING_X1, BME680_OVERSAMPLING_X2, BME680_OVERSAMPLING_X16);
	bme680.setIIRFilter(BME680_FILTER_3);
	bme680.setGasOn(300, 100); // 300 degree Celsius and 100 milliseconds

	bme680.setForcedMode();
}
String data = "";
void bme680_get()
{
  char oled_data[32] = {0};
	DEBUG_PRT("result: ");
	uint32_t i = 0;
	memset(m_lora_app_data.buffer, 0, LORAWAN_APP_DATA_BUFF_SIZE);
	m_lora_app_data.port = gAppPort;

	double temp = bme680.readTemperature();
	double pres = bme680.readPressure();
	double hum = bme680.readHumidity();

	uint32_t gas = bme680.readGasResistance();

	data = "Tem:" + String(temp) + "C " + "Hum:" + String(hum) + "% " + "Pres:" + String(pres) + "KPa " + "Gas:" + String(gas) + "Ohms";
	DEBUG_PLN(data);



	uint16_t t = temp * 100;
	uint16_t h = hum * 100;
	uint32_t pre = pres * 100;

	//result: T=28.25C, RH=50.00%, P=958.57hPa, G=100406 Ohms
	m_lora_app_data.buffer[i++] = 0x01;
	m_lora_app_data.buffer[i++] = (uint8_t)(t >> 8);
	m_lora_app_data.buffer[i++] = (uint8_t)t;
	m_lora_app_data.buffer[i++] = (uint8_t)(h >> 8);
	m_lora_app_data.buffer[i++] = (uint8_t)h;
	m_lora_app_data.buffer[i++] = (uint8_t)((pre & 0xFF000000) >> 24);
	m_lora_app_data.buffer[i++] = (uint8_t)((pre & 0x00FF0000) >> 16);
	m_lora_app_data.buffer[i++] = (uint8_t)((pre & 0x0000FF00) >> 8);
	m_lora_app_data.buffer[i++] = (uint8_t)(pre & 0x000000FF);
	m_lora_app_data.buffer[i++] = (uint8_t)((gas & 0xFF000000) >> 24);
	m_lora_app_data.buffer[i++] = (uint8_t)((gas & 0x00FF0000) >> 16);
	m_lora_app_data.buffer[i++] = (uint8_t)((gas & 0x0000FF00) >> 8);
	m_lora_app_data.buffer[i++] = (uint8_t)(gas & 0x000000FF);
	m_lora_app_data.buffsize = i;
	bme680.setForcedMode();
}
